\documentclass{naproche-library}
\libinput{preamble}
\begin{document}
\IfInputref{}{\setsectionlevel{section}}
\begin{smodule}[title=Symmetric Difference]{symmetric-difference.ftl}
\importmodule[libraries/foundations]{computation-laws-for-classes.ftl}
\symdef{SETsymdiff}{\,\triangle\,}

\begin{sfragment}{Definitions}
  \begin{definition}[forthel,id=FOUNDATIONS_03_7457594151010304]
    Let $A, B$ be classes.
    $\emph{A \SETsymdiff B} = (A \SETunion B) \SETdiff (A \SETintersect B)$.

    Let the \emph{symmetric difference} of $A$ and $B$ stand for $A \SETsymdiff B$.
  \end{definition}

  \begin{proposition}[forthel,id=FOUNDATIONS_03_4886447211413504]
    Let $A, B$ be classes.
    Then $A \SETsymdiff B \eq (A \SETdiff B) \SETunion (B \SETdiff A)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETsymdiff B \SETinclude (A \SETdiff B) \SETunion (B \SETdiff A)$.
      Let $u \in A \SETsymdiff B$.
      Then $u \in A \SETunion B$ and $u \notin A \SETintersect B$.
      Hence ($u \in A$ or $u \in B$) and not ($u \in A$ and $u \in B$).
      Thus ($u \in A$ or $u \in B$) and ($u \notin A$ or $u \notin B$).
      Therefore if $u \in A$ then $u \notin B$.
      If $u \in B$ then $u \notin A$.
      Then we have ($u \in A$ and $u \notin B$) or ($u \in B$ and $u \notin A$).
      Hence $u \in A \SETdiff B$ or $u \in B \SETdiff A$.
      Thus $u \in (A \SETdiff B) \SETunion (B \SETdiff A)$.
    End.

    Let us show that $((A \SETdiff B) \SETunion (B \SETdiff A)) \SETinclude A \SETsymdiff B$. %!
      Let $u \in (A \SETdiff B) \SETunion (B \SETdiff A)$.
      Then ($u \in A$ and $u \notin B$) or ($u \in B$ and $u \notin A$).
      If $u \in A$ and $u \notin B$ then $u \in A \SETunion B$ and $u \notin A \SETintersect B$.
      If $u \in B$ and $u \notin A$ then $u \in A \SETunion B$ and $u \notin A \SETintersect B$.
      Hence $u \in A \SETunion B$ and $u \notin A \SETintersect B$.
      Thus $u \in (A \SETunion B) \SETdiff (A \SETintersect B) \eq A \SETsymdiff B$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Computation Laws}
  \begin{sfragment}{Commutativity}
    \begin{proposition}[forthel,id=FOUNDATIONS_03_4518372049944576]
      Let $A, B$ be classes.
      Then $A \SETsymdiff B \eq B \SETsymdiff A$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff B
        \eq (A \SETunion B) \SETdiff (A \SETintersect B)
        \eq (B \SETunion A) \SETdiff (B \SETintersect A)
        \eq B \SETsymdiff A$.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Associativity}
    \begin{proposition}[forthel,id=FOUNDATIONS_03_8680845204258816]
      Let $A, B, C$ be classes.
      Then $(A \SETsymdiff B) \SETsymdiff C \eq A \SETsymdiff (B \SETsymdiff C)$.
    \end{proposition}
    \begin{proof}[forthel]
      Take a class $X$ such that $X \eq (((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C) \SETunion (C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A)))$.

      Take a class $Y$ such that $Y \eq (A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))) \SETunion (((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A)$.

      We have $A \SETsymdiff B \eq (A \SETdiff B) \SETunion (B \SETdiff A)$ and $B \SETsymdiff C \eq (B \SETdiff C) \SETunion (C \SETdiff B)$.
      Hence $(A \SETsymdiff B) \SETsymdiff C \eq X$ and $A \SETsymdiff (B \SETsymdiff C) \eq Y$.

      Let us show that (I) $X \SETinclude Y$.
        Let $x \in X$.

        (I 1) Case $x \in ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.
          Then $x \notin C$.

          (I 1a) Case $x \in A \SETdiff B$.
            Then $x \notin B \SETdiff C$ and $x \notin C \SETdiff B$. $x \in A$.
            Hence $x \in A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.
            Thus $x \in Y$.
          End.

          (I 1b) Case $x \in B \SETdiff A$.
            Then $x \in B \SETdiff C$.
            Hence $x \in (B \SETdiff C) \SETunion (C \SETdiff B)$. $x \notin A$.
            Thus $x \in ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.
            Therefore $x \in Y$.
          End.
        End.

        (I 2) Case $x \in C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.
          Then $x \in C$.
          $x \notin A \SETdiff B$ and $x \notin B \SETdiff A$.
          Hence not ($x \in A \SETdiff B$ or $x \in B \SETdiff A$).
          Thus not (($x \in A$ and $x \notin B$) or ($x \in B$ and $x \notin A$)).
          Therefore ($x \notin A$ or $x \in B$) and ($x \notin B$ or $x \in A$).

          (I 2a) Case $x \in A$.
            Then $x \in B$.
            Hence $x \notin (B \SETdiff C) \SETunion (C \SETdiff B)$.
            Thus $x \in A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.
            Therefore $x \in Y$.
          End.

          (I 2b) Case $x \notin A$.
            Then $x \notin B$.
            Hence $x \in C \SETdiff B$.
            Thus $x \in (B \SETdiff C) \SETunion (C \SETdiff B)$.
            Therefore $x \in ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.
            Then we have $x \in Y$.
          End.
        End.
      End.

      Let us show that (II) $Y \SETinclude X$.
        Let $y \in Y$.

        (II 1) Case $y \in A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.
          Then $y \in A$.
          $y \notin B \SETdiff C$ and $y \notin C \SETdiff B$.
          Hence not ($y \in B \SETdiff C$ or $y \in C \SETdiff B$).
          Thus not (($y \in B$ and $y \notin C$) or ($y \in C$ and $y \notin B$)).
          Therefore ($y \notin B$ or $y \in C$) and ($y \notin C$ or $y \in B$).

          (II 1a) Case $y \in B$.
            Then $y \in C$.
            $y \notin A \SETdiff B$ and $y \notin B \SETdiff A$.
            Hence $y \notin (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \in C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.
            Therefore $y \in X$.
          End.

          (II 1b) Case $y \notin B$.
            Then $y \notin C$.
            $y \in A \SETdiff B$.
            Hence $y \in (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \in ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.
            Therefore $y \in X$.
          End.
        End.

        (II 2) Case $y \in ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.
          Then $y \notin A$.

          (II 2a) Case $y \in B \SETdiff C$.
            Then $y \in B \SETdiff A$.
            Hence $y \in (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \in ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.
            Therefore $y \in X$.
          End.

          (II 2b) Case $y \in C \SETdiff B$.
            Then $y \in C$.
            $y \notin A \SETdiff B$ and $y \notin B \SETdiff A$.
            Hence $y \notin (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \in C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.
            Therefore $y \in X$.
          End.
        End.
      End.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Distributivity}
    \begin{proposition}[forthel,id=FOUNDATIONS_03_4119141910839296]
      Let $A, B, C$ be classes.
      Then $A \SETintersect (B \SETsymdiff C) \eq (A \SETintersect B) \SETsymdiff (A \SETintersect C)$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETintersect (B \SETsymdiff C)
        \eq A \SETintersect ((B \SETdiff C) \SETunion (C \SETdiff B))
        \eq (A \SETintersect (B \SETdiff C)) \SETunion (A \SETintersect (C \SETdiff B))$.

      $A \SETintersect (B \SETdiff C) \eq (A \SETintersect B) \SETdiff (A \SETintersect C)$.
      $A \SETintersect (C \SETdiff B) \eq (A \SETintersect C) \SETdiff (A \SETintersect B)$.

      Hence $A \SETintersect (B \SETsymdiff C)
        \eq ((A \SETintersect B) \SETdiff (A \SETintersect C)) \SETunion ((A \SETintersect C) \SETdiff (A \SETintersect B))
        \eq (A \SETintersect B) \SETsymdiff (A \SETintersect C)$.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Miscellaneous Rules}
    \begin{proposition}[forthel,id=FOUNDATIONS_03_7383417205293056]
      Let $A, B$ be classes.
      Then $A \SETinclude B \iff A \SETsymdiff B \eq B \SETdiff A$.
    \end{proposition}
    \begin{proof}[forthel]
      Case $A \SETinclude B$.
        Then $A \SETunion B \eq B$ and $A \SETintersect B \eq A$.
        Hence the thesis.
      End.

      Case $A \SETsymdiff B \eq B \SETdiff A$.
        Let $a \in A$.
        Then $a \notin B \SETdiff A$.
        Hence $a \notin A \SETsymdiff B$.
        Thus $a \notin A \SETunion B$ or $a \in A \SETintersect B$.
        Indeed $A \SETsymdiff B \eq (A \SETunion B) \SETdiff (A \SETintersect B)$.
        If $a \notin A \SETunion B$ then we have a contradiction.
        Therefore $a \in A \SETintersect B$.
        Then we have the thesis.
      End.
    \end{proof}

    \begin{proposition}[forthel,id=FOUNDATIONS_03_4490230937681920]
      Let $A, B, C$ be classes.
      Then $A \SETsymdiff B \eq A \SETsymdiff C \iff B \eq C$.
    \end{proposition}
    \begin{proof}[forthel]
      Case $A \SETsymdiff B \eq A \SETsymdiff C$.

        Let us show that $B \SETinclude C$.
          Let $b \in B$.

          Case $b \in A$.
            Then $b \notin A \SETsymdiff B$.
            Hence $b \notin A \SETsymdiff C$.
            Therefore $b \in A \SETintersect C$.
            Indeed $A \SETsymdiff C \eq (A \SETunion C) \SETdiff (A \SETintersect C)$.
            Hence $b \in C$.
          End.

          Case $b \notin A$.
            Then $b \in A \SETsymdiff B$.
            Indeed $b \in A \SETunion B$ and $b \notin A \SETintersect B$.
            Hence $b \in A \SETsymdiff C$.
            Thus $b \in A \SETunion C$ and $b \notin A \SETintersect C$.
            Therefore $b \in A$ or $b \in C$.
            Then we have the thesis.
          End.
        End.

        Let us show that $C \SETinclude B$.
          Let $c \in C$.

          Case $c \in A$.
            Then $c \notin A \SETsymdiff C$.
            Hence $c \notin A \SETsymdiff B$.
            Therefore $c \in A \SETintersect B$.
            Indeed $c \notin A \SETunion B$ or $c \in A \SETintersect B$.
            Hence $c \in B$.
          End.

          Case $c \notin A$.
            Then $c \in A \SETsymdiff C$.
            Indeed $c \in A \SETunion C$ and $c \notin A \SETintersect C$.
            Hence $c \in A \SETsymdiff B$.
            Thus $c \in A \SETunion B$ and $c \notin A \SETintersect B$.
            Therefore $c \in A$ or $c \in B$.
            Then we have the thesis.
          End.
        End.
      End.
    \end{proof}

    \begin{proposition}[forthel,id=FOUNDATIONS_03_4578696040022016]
      Let $A$ be a class.
      Then $A \SETsymdiff A \eq \SETempty$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff A
        \eq (A \SETunion A) \SETdiff (A \SETintersect A)
        \eq A \SETdiff A
        \eq \SETempty$.
    \end{proof}

    \begin{proposition}[forthel,id=FOUNDATIONS_03_6698730398941184]
      Let $A$ be a class.
      Then $A \SETsymdiff \SETempty \eq A$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff \SETempty
        \eq (A \SETunion \SETempty) \SETdiff (A \SETintersect \SETempty)
        \eq A \SETdiff \SETempty
        \eq A$.
    \end{proof}

    \begin{proposition}[forthel,id=FOUNDATIONS_03_6111806917443584]
      Let $A, B$ be classes.
      Then $A \eq B \iff A \SETsymdiff B \eq \SETempty$.
    \end{proposition}
    \begin{proof}[forthel]
      Case $A \eq B$.
        Then $A \SETsymdiff B
          \eq (A \SETunion A) \SETdiff (A \SETintersect A)
          \eq A \SETdiff A
          \eq \SETempty.$
        Hence the thesis.
      End.

      Case $A \SETsymdiff B \eq \SETempty$.
        Then $(A \SETunion B) \SETdiff (A \SETintersect B)$ is empty.
        Hence every element of $A \SETunion B$ is an element of $A \SETintersect B$.
        Thus for all objects $u$ if $u \in A$ or $u \in B$ then $u \in A$ and $u \in B$.
        Therefore every element of $A$ is an element of $B$.
        Every element of $B$ is an element of $A$.
        Then we have the thesis.
      End.
    \end{proof}
  \end{sfragment}
\end{sfragment}
\end{smodule}
\end{document}
