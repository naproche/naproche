\documentclass{stex}
\libusepackage{naproche}
\libinput{preamble}
\begin{document}
\begin{smodule}{symmetric-difference.ftl}
\importmodule[libraries/foundations]{computation-laws-for-classes.ftl}
\symdef{SETsymdiff}{\,\triangle\,}

\begin{sfragment}{Definitions}
  \begin{definition}[forthel]
    Let $A, B$ be classes.
    $\emph{A \SETsymdiff B} = (A \SETunion B) \SETdiff (A \SETintersect B)$.
    Let the \emph{symmetric difference of $A$ and $B$} stand for $A \SETsymdiff B$.
  \end{definition}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETsymdiff B \Eq (A \SETdiff B) \SETunion (B \SETdiff A)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETsymdiff B \SETinclude (A \SETdiff B) \SETunion (B \SETdiff A)$.
      Let $u \Elem A \SETsymdiff B$.
      Then $u \Elem A \SETunion B$ and $u \NotElem A \SETintersect B$.
      Hence ($u \Elem A$ or $u \Elem B$) and not ($u \Elem A$ and $u \Elem B$).
      Thus ($u \Elem A$ or $u \Elem B$) and ($u \NotElem A$ or $u \NotElem B$).
      Therefore if $u \Elem A$ then $u \NotElem B$.
      If $u \Elem B$ then $u \NotElem A$.
      Then we have ($u \Elem A$ and $u \NotElem B$) or ($u \Elem B$ and $u \NotElem A$).
      Hence $u \Elem A \SETdiff B$ or $u \Elem B \SETdiff A$.
      Thus $u \Elem (A \SETdiff B) \SETunion (B \SETdiff A)$.
    End.

    Let us show that $((A \SETdiff B) \SETunion (B \SETdiff A)) \SETinclude A \SETsymdiff B$. %!
      Let $u \Elem (A \SETdiff B) \SETunion (B \SETdiff A)$.
      Then ($u \Elem A$ and $u \NotElem B$) or ($u \Elem B$ and $u \NotElem A$).
      If $u \Elem A$ and $u \NotElem B$ then $u \Elem A \SETunion B$ and $u \NotElem A \SETintersect B$.
      If $u \Elem B$ and $u \NotElem A$ then $u \Elem A \SETunion B$ and $u \NotElem A \SETintersect B$.
      Hence $u \Elem A \SETunion B$ and $u \NotElem A \SETintersect B$.
      Thus $u \Elem (A \SETunion B) \SETdiff (A \SETintersect B) \Eq A \SETsymdiff B$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Computation Laws}
  \begin{sfragment}{Commutativity}
    \begin{proposition}[forthel]
      Let $A, B$ be classes.
      Then $A \SETsymdiff B \Eq B \SETsymdiff A$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff B
        \Eq (A \SETunion B) \SETdiff (A \SETintersect B)
        \Eq (B \SETunion A) \SETdiff (B \SETintersect A)
        \Eq B \SETsymdiff A$.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Associativity}
    \begin{proposition}[forthel]
      Let $A, B, C$ be classes.
      Then $(A \SETsymdiff B) \SETsymdiff C \Eq A \SETsymdiff (B \SETsymdiff C)$.
    \end{proposition}
    \begin{proof}[forthel]
      Take a class $X$ such that $X \Eq (((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C) \SETunion (C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A)))$.

      Take a class $Y$ such that $Y \Eq (A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))) \SETunion (((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A)$.

      We have $A \SETsymdiff B \Eq (A \SETdiff B) \SETunion (B \SETdiff A)$ and $B \SETsymdiff C \Eq (B \SETdiff C) \SETunion (C \SETdiff B)$.
      Hence $(A \SETsymdiff B) \SETsymdiff C \Eq X$ and $A \SETsymdiff (B \SETsymdiff C) \Eq Y$.

      Let us show that (I) $X \SETinclude Y$.
        Let $x \Elem X$.

        \begin{case}{$x \Elem ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.}
          Then $x \NotElem C$.

          \begin{case}{$x \Elem A \SETdiff B$.}
            Then $x \NotElem B \SETdiff C$ and $x \NotElem C \SETdiff B$. $x \Elem A$.
            Hence $x \Elem A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.
            Thus $x \Elem Y$.
          \end{case}

          \begin{case}{$x \Elem B \SETdiff A$.}
            Then $x \Elem B \SETdiff C$.
            Hence $x \Elem (B \SETdiff C) \SETunion (C \SETdiff B)$. $x \NotElem A$.
            Thus $x \Elem ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.
            Therefore $x \Elem Y$.
          \end{case}
        \end{case}

        \begin{case}{$x \Elem C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.}
          Then $x \Elem C$.
          $x \NotElem A \SETdiff B$ and $x \NotElem B \SETdiff A$.
          Hence not ($x \Elem A \SETdiff B$ or $x \Elem B \SETdiff A$).
          Thus not (($x \Elem A$ and $x \NotElem B$) or ($x \Elem B$ and $x \NotElem A$)).
          Therefore ($x \NotElem A$ or $x \Elem B$) and ($x \NotElem B$ or $x \Elem A$).

          \begin{case}{$x \Elem A$.}
            Then $x \Elem B$.
            Hence $x \NotElem (B \SETdiff C) \SETunion (C \SETdiff B)$.
            Thus $x \Elem A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.
            Therefore $x \Elem Y$.
          \end{case}

          \begin{case}{$x \NotElem A$.}
            Then $x \NotElem B$.
            Hence $x \Elem C \SETdiff B$.
            Thus $x \Elem (B \SETdiff C) \SETunion (C \SETdiff B)$.
            Therefore $x \Elem ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.
            Then we have $x \Elem Y$.
          \end{case}
        \end{case}
      End.

      Let us show that (II) $Y \SETinclude X$.
        Let $y \Elem Y$.

        \begin{case}{$y \Elem A \SETdiff ((B \SETdiff C) \SETunion (C \SETdiff B))$.}
          Then $y \Elem A$.
          $y \NotElem B \SETdiff C$ and $y \NotElem C \SETdiff B$.
          Hence not ($y \Elem B \SETdiff C$ or $y \Elem C \SETdiff B$).
          Thus not (($y \Elem B$ and $y \NotElem C$) or ($y \Elem C$ and $y \NotElem B$)).
          Therefore ($y \NotElem B$ or $y \Elem C$) and ($y \NotElem C$ or $y \Elem B$).

          \begin{case}{$y \Elem B$.}
            Then $y \Elem C$.
            $y \NotElem A \SETdiff B$ and $y \NotElem B \SETdiff A$.
            Hence $y \NotElem (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \Elem C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.
            Therefore $y \Elem X$.
          \end{case}

          \begin{case}{$y \NotElem B$.}
            Then $y \NotElem C$.
            $y \Elem A \SETdiff B$.
            Hence $y \Elem (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \Elem ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.
            Therefore $y \Elem X$.
          \end{case}
        \end{case}

        \begin{case}{$y \Elem ((B \SETdiff C) \SETunion (C \SETdiff B)) \SETdiff A$.}
          Then $y \NotElem A$.

          \begin{case}{$y \Elem B \SETdiff C$.}
            Then $y \Elem B \SETdiff A$.
            Hence $y \Elem (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \Elem ((A \SETdiff B) \SETunion (B \SETdiff A)) \SETdiff C$.
            Therefore $y \Elem X$.
          \end{case}

          \begin{case}{$y \Elem C \SETdiff B$.}
            Then $y \Elem C$.
            $y \NotElem A \SETdiff B$ and $y \NotElem B \SETdiff A$.
            Hence $y \NotElem (A \SETdiff B) \SETunion (B \SETdiff A)$.
            Thus $y \Elem C \SETdiff ((A \SETdiff B) \SETunion (B \SETdiff A))$.
            Therefore $y \Elem X$.
          \end{case}
        \end{case}
      End.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Distributivity}
    \begin{proposition}[forthel]
      Let $A, B, C$ be classes.
      Then $A \SETintersect (B \SETsymdiff C) \Eq (A \SETintersect B) \SETsymdiff (A \SETintersect C)$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETintersect (B \SETsymdiff C)
        \Eq A \SETintersect ((B \SETdiff C) \SETunion (C \SETdiff B))
        \Eq (A \SETintersect (B \SETdiff C)) \SETunion (A \SETintersect (C \SETdiff B))$.

      $A \SETintersect (B \SETdiff C) \Eq (A \SETintersect B) \SETdiff (A \SETintersect C)$.
      $A \SETintersect (C \SETdiff B) \Eq (A \SETintersect C) \SETdiff (A \SETintersect B)$.

      Hence $A \SETintersect (B \SETsymdiff C)
        \Eq ((A \SETintersect B) \SETdiff (A \SETintersect C)) \SETunion ((A \SETintersect C) \SETdiff (A \SETintersect B))
        \Eq (A \SETintersect B) \SETsymdiff (A \SETintersect C)$.
    \end{proof}
  \end{sfragment}

  \begin{sfragment}{Miscellaneous Rules}
    \begin{proposition}[forthel]
      Let $A, B$ be classes.
      Then $A \SETinclude B \Iff A \SETsymdiff B \Eq B \SETdiff A$.
    \end{proposition}
    \begin{proof}[forthel]
      \begin{case}{$A \SETinclude B$.}
        Then $A \SETunion B \Eq B$ and $A \SETintersect B \Eq A$.
        Hence the thesis.
      \end{case}

      \begin{case}{$A \SETsymdiff B \Eq B \SETdiff A$.}
        Let $a \Elem A$.
        Then $a \NotElem B \SETdiff A$.
        Hence $a \NotElem A \SETsymdiff B$.
        Thus $a \NotElem A \SETunion B$ or $a \Elem A \SETintersect B$.
        Indeed $A \SETsymdiff B \Eq (A \SETunion B) \SETdiff (A \SETintersect B)$.
        If $a \NotElem A \SETunion B$ then we have a contradiction.
        Therefore $a \Elem A \SETintersect B$.
        Then we have the thesis.
      \end{case}
    \end{proof}

    \begin{proposition}[forthel]
      Let $A, B, C$ be classes.
      Then $A \SETsymdiff B \Eq A \SETsymdiff C \Iff B \Eq C$.
    \end{proposition}
    \begin{proof}[forthel]
      \begin{case}{$A \SETsymdiff B \Eq A \SETsymdiff C$.}

        Let us show that $B \SETinclude C$.
          Let $b \Elem B$.

          \begin{case}{$b \Elem A$.}
            Then $b \NotElem A \SETsymdiff B$.
            Hence $b \NotElem A \SETsymdiff C$.
            Therefore $b \Elem A \SETintersect C$.
            Indeed $A \SETsymdiff C \Eq (A \SETunion C) \SETdiff (A \SETintersect C)$.
            Hence $b \Elem C$.
          \end{case}

          \begin{case}{$b \NotElem A$.}
            Then $b \Elem A \SETsymdiff B$.
            Indeed $b \Elem A \SETunion B$ and $b \NotElem A \SETintersect B$.
            Hence $b \Elem A \SETsymdiff C$.
            Thus $b \Elem A \SETunion C$ and $b \NotElem A \SETintersect C$.
            Therefore $b \Elem A$ or $b \Elem C$.
            Then we have the thesis.
          \end{case}
        End.

        Let us show that $C \SETinclude B$.
          Let $c \Elem C$.

          \begin{case}{$c \Elem A$.}
            Then $c \NotElem A \SETsymdiff C$.
            Hence $c \NotElem A \SETsymdiff B$.
            Therefore $c \Elem A \SETintersect B$.
            Indeed $c \NotElem A \SETunion B$ or $c \Elem A \SETintersect B$.
            Hence $c \Elem B$.
          \end{case}

          \begin{case}{$c \NotElem A$.}
            Then $c \Elem A \SETsymdiff C$.
            Indeed $c \Elem A \SETunion C$ and $c \NotElem A \SETintersect C$.
            Hence $c \Elem A \SETsymdiff B$.
            Thus $c \Elem A \SETunion B$ and $c \NotElem A \SETintersect B$.
            Therefore $c \Elem A$ or $c \Elem B$.
            Then we have the thesis.
          \end{case}
        End.
      \end{case}
    \end{proof}

    \begin{proposition}[forthel]
      Let $A$ be a class.
      Then $A \SETsymdiff A \Eq \SETempty$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff A
        \Eq (A \SETunion A) \SETdiff (A \SETintersect A)
        \Eq A \SETdiff A
        \Eq \SETempty$.
    \end{proof}

    \begin{proposition}[forthel]
      Let $A$ be a class.
      Then $A \SETsymdiff \SETempty \Eq A$.
    \end{proposition}
    \begin{proof}[forthel]
      $A \SETsymdiff \SETempty
        \Eq (A \SETunion \SETempty) \SETdiff (A \SETintersect \SETempty)
        \Eq A \SETdiff \SETempty
        \Eq A$.
    \end{proof}

    \begin{proposition}[forthel]
      Let $A, B$ be classes.
      Then $A \Eq B \Iff A \SETsymdiff B \Eq \SETempty$.
    \end{proposition}
    \begin{proof}[forthel]
      \begin{case}{$A \Eq B$.}
        Then $A \SETsymdiff B
          \Eq (A \SETunion A) \SETdiff (A \SETintersect A)
          \Eq A \SETdiff A
          \Eq \SETempty.$
        Hence the thesis.
      \end{case}

      \begin{case}{$A \SETsymdiff B \Eq \SETempty$.}
        Then $(A \SETunion B) \SETdiff (A \SETintersect B)$ is empty.
        Hence every element of $A \SETunion B$ is an element of $A \SETintersect B$.
        Thus for all objects $u$ if $u \Elem A$ or $u \Elem B$ then $u \Elem A$ and $u \Elem B$.
        Therefore every element of $A$ is an element of $B$.
        Every element of $B$ is an element of $A$.
        Then we have the thesis.
      \end{case}
    \end{proof}
  \end{sfragment}
\end{sfragment}
\end{smodule}
\end{document}
