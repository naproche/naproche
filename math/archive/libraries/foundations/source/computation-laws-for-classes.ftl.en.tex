\documentclass{stex}
\libusepackage{naproche}
\libinput{preamble}
\begin{document}
\begin{smodule}{computation-laws-for-classes.ftl}
\importmodule[libraries/foundations]{classes.ftl}

\begin{sfragment}{Commutativity of Union and Intersection}
  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETunion B \Eq B \SETunion A$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETunion B \SETinclude B \SETunion A$.
      Let $x \Elem A \SETunion B$.
      Then $x \Elem A$ or $x \Elem B$.
      Hence $x \Elem B$ or $x \Elem A$.
      Thus $x \Elem B \SETunion A$.
    End.

    Let us show that $B \SETunion A \SETinclude A \SETunion B$.
      Let $x \Elem B \SETunion A$.
      Then $x \Elem B$ or $x \Elem A$.
      Hence $x \Elem A$ or $x \Elem B$.
      Thus $x \Elem A \SETunion B$.
    End.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETintersect B \Eq B \SETintersect A$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETintersect B \SETinclude B \SETintersect A$.
      Let $x \Elem A \SETintersect B$.
      Then $x \Elem A$ and $x \Elem B$.
      Hence $x \Elem B$ and $x \Elem A$.
      Thus $x \Elem B \SETintersect A$.
    End.

    Let us show that $B \SETintersect A \SETinclude A \SETintersect B$.
      Let $x \Elem B \SETintersect A$.
      Then $x \Elem B$ and $x \Elem A$.
      Hence $x \Elem A$ and $x \Elem B$.
      Thus $x \Elem A \SETintersect B$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Associativity of Union and Intersection}
  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $(A \SETunion B) \SETunion C \Eq A \SETunion (B \SETunion C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $((A \SETunion B) \SETunion C) \SETinclude A \SETunion (B \SETunion C)$. %!
      Let $x \Elem (A \SETunion B) \SETunion C$.
      Then $x \Elem A \SETunion B$ or $x \Elem C$.
      Hence $x \Elem A$ or $x \Elem B$ or $x \Elem C$.
      Thus $x \Elem A$ or $x \Elem (B \SETunion C)$.
      Therefore $x \Elem A \SETunion (B \SETunion C)$.
    End.

    Let us show that $A \SETunion (B \SETunion C) \SETinclude (A \SETunion B) \SETunion C$.
      Let $x \Elem A \SETunion (B \SETunion C)$.
      Then $x \Elem A$ or $x \Elem B \SETunion C$.
      Hence $x \Elem A$ or $x \Elem B$ or $x \Elem C$.
      Thus $x \Elem A \SETunion B$ or $x \Elem C$.
      Therefore $x \Elem (A \SETunion B) \SETunion C$.
    End.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $(A \SETintersect B) \SETintersect C \Eq A \SETintersect (B \SETintersect C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $((A \SETintersect B) \SETintersect C) \SETinclude A \SETintersect (B \SETintersect C)$. %!
      Let $x \Elem (A \SETintersect B) \SETintersect C$.
      Then $x \Elem A \SETintersect B$ and $x \Elem C$.
      Hence $x \Elem A$ and $x \Elem B$ and $x \Elem C$.
      Thus $x \Elem A$ and $x \Elem (B \SETintersect C)$.
      Therefore$ x \Elem A \SETintersect (B \SETintersect C)$.
    End.

    Let us show that $A \SETintersect (B \SETintersect C) \SETinclude (A \SETintersect B) \SETintersect C$.
      Let $x \Elem A \SETintersect (B \SETintersect C)$.
      Then $x \Elem A$ and $x \Elem B \SETintersect C$.
      Hence $x \Elem A$ and $x \Elem B$ and $x \Elem C$.
      Thus $x \Elem A \SETintersect B$ and $x \Elem C$.
      Therefore $x \Elem (A \SETintersect B) \SETintersect C$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Distributivity of Union and Intersection}
  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $A \SETintersect (B \SETunion C) \Eq (A \SETintersect B) \SETunion (A \SETintersect C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETintersect (B \SETunion C) \SETinclude (A \SETintersect B) \SETunion (A \SETintersect C)$.
      Let $x \Elem A \SETintersect (B \SETunion C)$.
      Then $x \Elem A$ and $x \Elem B \SETunion C$.
      Hence $x \Elem A$ and ($x \Elem B$ or $x \Elem C$).
      Thus ($x \Elem A$ and $x \Elem B$) or ($x \Elem A$ and $x \Elem C$).
      Therefore $x \Elem A \SETintersect B$ or $x \Elem A \SETintersect C$.
      Hence $x \Elem (A \SETintersect B) \SETunion (A \SETintersect C)$.
    End.

    Let us show that $((A \SETintersect B) \SETunion (A \SETintersect C)) \SETinclude A \SETintersect (B \SETunion C)$. %!
      Let $x \Elem (A \SETintersect B) \SETunion (A \SETintersect C)$.
      Then $x \Elem A \SETintersect B$ or $x \Elem A \SETintersect C$.
      Hence ($x \Elem A$ and $x \Elem B$) or ($x \Elem A$ and $x \Elem C$).
      Thus $x \Elem A$ and ($x \Elem B$ or $x \Elem C$).
      Therefore $x \Elem A$ and $x \Elem B \SETunion C$.
      Hence$ x \Elem A \SETintersect (B \SETunion C)$.
    End.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $A \SETunion (B \SETintersect C) \Eq (A \SETunion B) \SETintersect (A \SETunion C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETunion (B \SETintersect C) \SETinclude (A \SETunion B) \SETintersect (A \SETunion C)$.
      Let $x \Elem A \SETunion (B \SETintersect C)$.
      Then $x \Elem A$ or $x \Elem B \SETintersect C$.
      Hence $x \Elem A$ or ($x \Elem B$ and $x \Elem C$).
      Thus ($x \Elem A$ or $x \Elem B$) and ($x \Elem$ A or $x \Elem C$).
      Therefore $x \Elem A \SETunion B$ and $x \Elem A \SETunion C$.
      Hence $x \Elem (A \SETunion B) \SETintersect (A \SETunion C)$.
    End.

    Let us show that $((A \SETunion B) \SETintersect (A \SETunion C)) \SETinclude A \SETunion (B \SETintersect C)$. %!
      Let $x \Elem (A \SETunion B) \SETintersect (A \SETunion C)$.
      Then $x \Elem A \SETunion B$ and $x \Elem A \SETunion C$.
      Hence ($x \Elem A$ or $x \Elem B$) and ($x \Elem A$ or $x \Elem C$).
      Thus $x \Elem A$ or ($x \Elem B$ and $x \Elem C$).
      Therefore $x \Elem A$ or $x \Elem B \SETintersect C$.
      Hence $x \Elem A \SETunion (B \SETintersect C)$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Idempocy Laws for Union and Intersection}
  \begin{proposition}[forthel]
    Let $A$ be a class.
    Then $A \SETunion A \Eq A$.
  \end{proposition}
  \begin{proof}[forthel]
    $A \SETunion A = \CClass{x}{x \Elem A\text{ or }x \Elem A}$.
    Hence $A \SETunion A = \CClass{x}{x \Elem A}$.
    Thus $A \SETunion A \Eq A$.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A$ be a class.
    Then $A \SETintersect A \Eq A$.
  \end{proposition}
  \begin{proof}[forthel]
    $A \SETintersect A = \CClass{x}{x \Elem A\text{ and }x \Elem A}$.
    Hence $A \SETintersect A = \CClass{x}{x \Elem A}$.
    Thus $A \SETintersect A \Eq A$.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Distributivity of Complement}
  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $A \SETdiff (B \SETintersect C) \Eq (A \SETdiff B) \SETunion (A \SETdiff C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETdiff (B \SETintersect C) \SETinclude (A \SETdiff B) \SETunion (A \SETdiff C)$.
      Let $x \Elem A \SETdiff (B \SETintersect C)$.
      Then $x \Elem A$ and $x \NotElem B \SETintersect C$.
      Hence it is wrong that ($x \Elem B$ and $x \Elem C$).
      Thus $x \NotElem B$ or $x \NotElem C$.
      Therefore $x \Elem A$ and ($x \NotElem B$ or $x \NotElem C$).
      Then ($x \Elem A$ and $x \NotElem B$) or ($x \Elem A$ and $x \NotElem C$).
      Hence $x \Elem A \SETdiff B$ or $x \Elem A \SETdiff C$.
      Thus $x \Elem (A \SETdiff B) \SETunion (A \SETdiff C)$.
    End.

    Let us show that $((A \SETdiff B) \SETunion (A \SETdiff C)) \SETinclude A \SETdiff (B \SETintersect C)$. %!
      Let $x \Elem (A \SETdiff B) \SETunion (A \SETdiff C)$.
      Then $x \Elem A \SETdiff B$ or $x \Elem A \SETdiff C$.
      Hence ($x \Elem A$ and $x \NotElem B$) or ($x \Elem A$ and $x \NotElem C$).
      Thus $x \Elem A$ and ($x \NotElem B$ or $x \NotElem C$).
      Therefore $x \Elem A$ and not ($x \Elem B$ and $x \Elem C$).
      Then $x \Elem A$ and not $x \Elem B \SETintersect C$.
      Hence $x \Elem A \SETdiff (B \SETintersect C)$.
    End.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $A \SETdiff (B \SETunion C) \Eq (A \SETdiff B) \SETintersect (A \SETdiff C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETdiff (B \SETunion C) \SETinclude (A \SETdiff B) \SETintersect (A \SETdiff C)$.
      Let $x \Elem A \SETdiff (B \SETunion C)$.
      Then $x \Elem A$ and $x \NotElem B \SETunion C$.
      Hence it is wrong that ($x \Elem B$ or $x \Elem C$).
      Thus $x \NotElem B$ and $x \NotElem C$.
      Therefore $x \Elem A$ and ($x \NotElem B$ and $x \NotElem C$).
      Then ($x \Elem A$ and $x \NotElem B$) and ($x \Elem A$ and $x \NotElem C$).
      Hence $x \Elem A \SETdiff B$ and $x \Elem A \SETdiff C$.
      Thus $x \Elem (A \SETdiff B) \SETintersect (A \SETdiff C)$.
    End.

    Let us show that $((A \SETdiff B) \SETintersect (A \SETdiff C)) \SETinclude A \SETdiff (B \SETunion C)$. %!
      Let $x \Elem (A \SETdiff B) \SETintersect (A \SETdiff C)$.
      Then $x \Elem A \SETdiff B$ and $x \Elem A \SETdiff C$.
      Hence ($x \Elem A$ and $x \NotElem B$) and ($x \Elem A$ and $x \NotElem C$).
      Thus $x \Elem A$ and ($x \NotElem B$ and $x \NotElem C$).
      Therefore $x \Elem A$ and not ($x \Elem B$ or $x \Elem C$).
      Then $x \Elem A$ and not $x \Elem B \SETunion C$.
      Hence $x \Elem A \SETdiff (B \SETunion C)$.
    End.
  \end{proof}
\end{sfragment}

\begin{sfragment}{Subclass Laws}
  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETinclude A \SETunion B$.
  \end{proposition}
  \begin{proof}[forthel]
    Let $x \Elem A$.
    Then $x \Elem A$ or $x \Elem B$.
    Hence $x \Elem A \SETunion B$.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETintersect B \SETinclude A$.
  \end{proposition}
  \begin{proof}[forthel]
    Let $x \Elem A \SETintersect B$.
    Then $x \Elem A$ and $x \Elem B$.
    Hence $x \Elem A$.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETinclude B$ iff $A \SETunion B \Eq B$.
  \end{proposition}
  \begin{proof}[forthel]
    \begin{case}{$A \SETinclude B$.}

      Let us show that $A \SETunion B \SETinclude B$.
        Let $x \Elem A \SETunion B$.
        Then $x \Elem A$ or $x \Elem B$.
        If $x \Elem A$ then $x \Elem B$.
        Hence $x \Elem B$.
      End.

      Let us show that $B \SETinclude A \SETunion B$.
        Let $x \Elem B$.
        Then $x \Elem A$ or $x \Elem B$.
        Hence $x \Elem A \SETunion B$.
      End.
    \end{case}

    \begin{case}{$A \SETunion B \Eq B$.}
      Let $x \Elem A$.
      Then $x \Elem A$ or $x \Elem B$.
      Hence $x \Elem A \SETunion B \Eq B$.
    \end{case}
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETinclude B$ iff $A \SETintersect B \Eq A$.
  \end{proposition}
  \begin{proof}[forthel]
    \begin{case}{$A \SETinclude B$.}

      Let us show that $A \SETintersect B \SETinclude A$.
        Let $x \Elem A \SETintersect B$.
        Then $x \Elem A$ and $x \Elem B$.
        Hence $x \Elem A$.
      End.

      Let us show that $A \SETinclude A \SETintersect B$.
        Let $x \Elem A$.
        Then $x \Elem B$.
        Hence $x \Elem A$ and $x \Elem B$.
        Thus $x \Elem A \SETintersect B$.
      End.
    \end{case}

    \begin{case}{$A \SETintersect B \Eq A$.}
      Let $x \Elem A$.
      Then $x \Elem A \SETintersect B$.
      Hence $x \Elem A$ and $x \Elem B$.
      Thus $x \Elem B$.
    \end{case}
  \end{proof}
\end{sfragment}

\begin{sfragment}{Complement Laws}
  \begin{proposition}[forthel]
    Let $A$ be a class.
    Then $A \SETdiff A \Eq \SETempty$.
  \end{proposition}
  \begin{proof}[forthel]
    $A \SETdiff A$ has no elements.
    Indeed $A \SETdiff A = \CClass{x}{x \Elem A\text{ and }x \NotElem A}$.
    Hence the thesis.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A$ be a class.
    Then $A \SETdiff \SETempty \Eq A$.
  \end{proposition}
  \begin{proof}[forthel]
    $A \SETdiff \SETempty = \CClass{x}{x \Elem A\text{ and }x \NotElem \SETempty}$.
    No element is an element of $\SETempty$.
    Hence $A \SETdiff \SETempty = \CClass{x}{x \Elem A}$.
    Then we have the thesis.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $A \SETdiff (A \SETdiff B) \Eq A \SETintersect B$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETdiff (A \SETdiff B) \SETinclude A \SETintersect B$.
      Let $x \Elem A \SETdiff (A \SETdiff B)$.
      Then $x \Elem A$ and $x \NotElem A \SETdiff B$.
      Hence $x \NotElem A$ or $x \Elem B$.
      Thus $x \Elem B$.
      Therefore $x \Elem A \SETintersect B$.
    End.

    Let us show that $A \SETintersect B \SETinclude A \SETdiff (A \SETdiff B)$.
      Let $x \Elem A \SETintersect B$.
      Then $x \Elem A$ and $x \Elem B$.
      Hence $x \NotElem A$ or $x \Elem B$.
      Thus $x \NotElem A \SETdiff B$.
      Therefore $x \Elem A \SETdiff (A \SETdiff B)$.
    End.
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B$ be classes.
    Then $B \SETinclude A$ iff $A \SETdiff (A \SETdiff B) \Eq B$.
  \end{proposition}
  \begin{proof}[forthel]
    \begin{case}{$B \SETinclude A$.} \end{case}

    \begin{case}{$A \SETdiff (A \SETdiff B) \Eq B$.}
      Then every element of $B$ is an element of $A \SETdiff (A \SETdiff B)$.
      Thus every element of $B$ is an element of $A$.
      Then we have the thesis.
    \end{case}
  \end{proof}

  \begin{proposition}[forthel]
    Let $A, B, C$ be classes.
    Then $A \SETintersect (B \SETdiff C) \Eq (A \SETintersect B) \SETdiff (A \SETintersect C)$.
  \end{proposition}
  \begin{proof}[forthel]
    Let us show that $A \SETintersect (B \SETdiff C) \SETinclude (A \SETintersect B) \SETdiff (A \SETintersect C)$.
      Let $x \Elem A \SETintersect (B \SETdiff C)$.
      Then $x \Elem A$ and $x \Elem B \SETdiff C$.
      Hence $x \Elem A$ and $x \Elem B$.
      Thus $x \Elem A \SETintersect B$ and $x \NotElem C$.
      Therefore $x \NotElem A \SETintersect C$.
      Then we have $x \Elem (A \SETintersect B) \SETdiff (A \SETintersect C)$.
    End.

    Let us show that $((A \SETintersect B) \SETdiff (A \SETintersect C)) \SETinclude A \SETintersect (B \SETdiff C)$. %!
      Let $x \Elem (A \SETintersect B) \SETdiff (A \SETintersect C)$.
      Then $x \Elem A$ and $x \Elem B$.
      $x \NotElem A \SETintersect C$.
      Hence $x \NotElem C$.
      Thus $x \Elem B \SETdiff C$.
      Therefore $x \Elem A \SETintersect (B \SETdiff C)$.
    End.
  \end{proof}
\end{sfragment}
\end{smodule}
\end{document}
