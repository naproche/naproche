\documentclass[../../set-theory.ftl.tex]{subfiles}

\begin{document}
  \section{The symmetric difference}

  \begin{forthel}
    [readtex \path{set-theory/sections/01_sets/01_sets.ftl.tex}]
  \end{forthel}

  \begin{forthel}
  Let $u,v,w$ denote elements.
  Let $x,y,z$ denote sets.
  \end{forthel}

  \subsection{Definition}

  \begin{forthel}
    \begin{definition}
      $x \symdiff y = (x \cup y) \setminus (x \cap y)$.
    \end{definition}

    Let the symmetric difference of $x$ and $y$ stand for $x \symdiff y$.

    \begin{lemma}
      $x \symdiff y$ is a set.
    \end{lemma}
    \begin{proof}
      $x$ and $y$ are sets.
      Hence $x \cup y$ and $x \cap y$ are sets.
      Thus $(x \cup y) \setminus (x \cap y)$ is a set.
      Therefore $x \symdiff y$ is a set.
    \end{proof}

    \begin{proposition}\label{SetTheory_01_04_470605}
      $x \symdiff y = (x \setminus y) \cup (y \setminus x)$.
    \end{proposition}
    \begin{proof}
      Let us show that $x \symdiff y \subseteq (x \setminus y) \cup
      (y \setminus x)$.
        Let $u \in x \symdiff y$.
        Then $u \in x \cup y$ and $u \notin x \cap y$.
        Hence ($u \in x$ or $u \in y$) and not ($u \in x$ and $u \in y$).
        Thus ($u \in x$ or $u \in y$) and ($u \notin x$ or $u \notin y$).
        Therefore if $u \in x$ then $u \notin y$.
        If $u \in y$ then $u \notin x$.
        Then we have ($u \in x$ and $u \notin y$) or ($u \in y$ and
        $u \notin x$).
        Hence $u \in x \setminus y$ or $u \in y \setminus x$.
        Thus $u \in (x \setminus y) \cup (y \setminus x)$.
      End.

      Let us show that $((x \setminus y) \cup (y \setminus x)) \subseteq
      x \symdiff y$.
        % Note that we have to put the LHS in parentheses.
        Let $u \in (x \setminus y) \cup (y \setminus x)$.
        Then ($u \in x$ and $u \notin y$) or ($u \in y$ and $u \notin x$).
        If $u \in x$ and $u \notin y$ then $u \in x \cup y$ and
        $u \notin x \cap y$.
        If $u \in y$ and $u \notin x$ then $u \in x \cup y$ and
        $u \notin x \cap y$.
        Hence $u \in x \cup y$ and $u \notin x \cap y$.
        Thus $u \in (x \cup y) \setminus (x \cap y) = x \symdiff y$.
      End.
    \end{proof}
  \end{forthel}


  \subsection{Computation laws}

  \begin{forthel}
    \begin{proposition}\label{SetTheory_01_04_688675}
      \[ x \symdiff y = y \symdiff x. \]
    \end{proposition}
    \begin{proof}
      $x \symdiff y
      = (x \cup y) \setminus (x \cap y)
      = (y \cup x) \setminus (y \cap x)
      = y \symdiff x$.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_606646}
      \[ ((x \symdiff y) \symdiff z) = x \symdiff (y \symdiff z). \]
      % Again parentheses on the LHS.
    \end{proposition}
    \begin{proof}
      Take $A = (((x \setminus y) \cup (y \setminus x)) \setminus z) \cup (z \setminus ((x \setminus y) \cup (y \setminus x)))$.

      Take $B = (x \setminus ((y \setminus z) \cup (z \setminus y))) \cup (((y \setminus z) \cup (z \setminus y)) \setminus x)$.

      We have $x \symdiff y = (x \setminus y) \cup (y \setminus x)$ and $y \symdiff z = (y \setminus z) \cup (z \setminus y)$.
      Hence $(x \symdiff y) \symdiff z = A$ and $x \symdiff (y \symdiff z) = B$.

      Let us show that (A) $A \subseteq B$.
        Let $u \in A$.

        (A 1) Case $u \in ((x \setminus y) \cup (y \setminus x)) \setminus z$.
          Then $u \notin z$.

          (A 1a) Case $u \in x \setminus y$.
            Then $u \notin y \setminus z$ and $u \notin z \setminus y$.
            $u \in x$.
            Hence $u \in x \setminus ((y \setminus z) \cup (z \setminus y))$.
            Thus $u \in B$.
          End.

          (A 1b) Case $u \in y \setminus x$.
            Then $u \in y \setminus z$.
            Hence $u \in (y \setminus z) \cup (z \setminus y)$.
            $u \notin x$.
            Thus $u \in ((y \setminus z) \cup (z \setminus y)) \setminus x$.
            Therefore $u \in B$.
          End.
        End.

        (A 2) Case $u \in z \setminus ((x \setminus y) \cup (y \setminus x))$.
          Then $u \in z$. $u \notin x \setminus y$ and $u \notin y \setminus x$.
          Hence not ($u \in x \setminus y$ or $u \in y \setminus x$).
          Thus not (($u \in x$ and $u \notin y$) or ($u \in y$ and
          $u \notin x$)).
          Therefore ($u \notin x$ or $u \in y$) and ($u \notin y$ or $u \in x$).

          (A 2a) Case $u \in x$.
            Then $u \in y$.
            Hence $u \notin (y \setminus z) \cup (z \setminus y)$.
            Thus $u \in x \setminus ((y \setminus z) \cup (z \setminus y))$.
            Therefore $u \in B$.
          End.

          (A 2b) Case $u \notin x$.
            Then $u \notin y$.
            Hence $u \in z \setminus y$.
            Thus $u \in (y \setminus z) \cup (z \setminus y)$.
            Therefore $u \in ((y \setminus z) \cup (z \setminus y)) \setminus x$.
            Then we have $u \in B$.
          End.
        End.
      End.

      Let us show that (B) $B \subseteq A$.
        Let $u \in B$.

        (B 1) Case $u \in x \setminus ((y \setminus z) \cup (z \setminus y))$.
          Then $u \in x$.
          $u \notin y \setminus z$ and $u \notin z \setminus y$.
          Hence not ($u \in y \setminus z$ or $u \in z \setminus y$).
          Thus not (($u \in y$ and $u \notin z$) or ($u \in z$ and
          $u \notin y$)).
          Therefore ($u \notin y$ or $u \in z$) and ($u \notin z$ or $u \in y$).

          (B 1a) Case $u \in y$.
            Then $u \in z$.
            $u \notin x \setminus y$ and $u \notin y \setminus x$.
            Hence $u \notin (x \setminus y) \cup (y \setminus x)$.
            Thus $u \in z \setminus ((x \setminus y) \cup (y \setminus x))$.
            Therefore $u \in A$.
          End.

          (B 1b) Case $u \notin y$.
            Then $u \notin z$.
            $u \in x \setminus y$.
            Hence $u \in (x \setminus y) \cup (y \setminus x)$.
            Thus $u \in ((x \setminus y) \cup (y \setminus x)) \setminus z$.
            Therefore $u \in A$.
          End.
        End.

        (B 2) Case $u \in ((y \setminus z) \cup (z \setminus y)) \setminus x$.
          Then $u \notin x$.

          (B 2a) Case $u \in y \setminus z$.
            Then $u \in y \setminus x$.
            Hence $u \in (x \setminus y) \cup (y \setminus x)$.
            Thus $u \in ((x \setminus y) \cup (y \setminus x)) \setminus z$.
            Therefore $u \in A$.
          End.

          (B 2b) Case $u \in z \setminus y$.
            Then $u \in z$.
            $u \notin x \setminus y$ and $u \notin y \setminus x$.
            Hence $u \notin (x \setminus y) \cup (y \setminus x)$.
            Thus $u \in z \setminus ((x \setminus y) \cup (y \setminus x))$.
            Therefore $u \in A$.
          End.
        End.
      End.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_751668}
      \[ x \cap (y \symdiff z) = (x \cap y) \symdiff (x \cap z). \]
    \end{proposition}
    \begin{proof}
      $x \cap (y \symdiff z) = x \cap ((y \setminus z) \cup (z \setminus y)) =
      (x \cap (y \setminus z)) \cup (x \cap (z \setminus y))$.

      $x \cap (y \setminus z) = (x \cap y) \setminus (x \cap z)$.
      $x \cap (z \setminus y) = (x \cap z) \setminus (x \cap y)$.

      Hence $x \cap (y \symdiff z) = ((x \cap y) \setminus (x \cap z)) \cup
      ((x \cap z) \setminus (x \cap y)) = (x \cap y) \symdiff (x \cap z)$.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_420961}
      \[ x \subseteq y \iff x \symdiff y = y \setminus x. \]
    \end{proposition}
    \begin{proof}
      Case $x \subseteq y$.
        Then $x \cup y = y$ and $x \cap y = x$.
        Hence the thesis.
      End.

      Case $x \symdiff y = y \setminus x$.
        Let $u \in x$.
        Then $u \notin y \setminus x$.
        Hence $u \notin x \symdiff y$.
        Thus $u \notin x \cup y$ or $u \in x \cap y$.
        Indeed $x \symdiff y = (x \cup y) \setminus (x \cap y)$.
        If $u \notin x \cup y$ then we have a contradiction.
        Therefore $u \in x \cap y$.
        Then we have the thesis.
      End.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_241267}
      \[ x \symdiff y = x \symdiff z \iff y = z. \]
    \end{proposition}
    \begin{proof}
      Case $x \symdiff y = x \symdiff z$.

        Let us show that $y \subseteq z$.
          Let $u \in y$.

          Case $u \in x$.
            Then $u \notin x \symdiff y$.
            Hence $u \notin x \symdiff z$.
            Therefore $u \in x \cap z$.
            Indeed $x \symdiff z = (x \cup z) \setminus (x \cap z)$.
            Hence $u \in z$.
          End.

          Case $u \notin x$.
            Then $u \in x \symdiff y$.
            Indeed $u \in x \cup y$ and $u \notin x \cap y$.
            Hence $u \in x \symdiff z$.
            Thus $u \in x \cup z$ and $u \notin x \cap z$.
            Therefore $u \in x$ or $u \in z$.
            Then we have the thesis.
          End.
        End.

        Let us show that $z \subseteq y$.
          Let $u \in z$.

          Case $u \in x$.
            Then $u \notin x \symdiff z$.
            Hence $u \notin x \symdiff y$.
            Therefore $u \in x \cap y$.
            Indeed $u \notin x \cup y$ or $u \in x \cap y$.
            Hence $u \in y$.
          End.

          Case $u \notin x$.
            Then $u \in x \symdiff z$.
            Indeed $u \in x \cup z$ and $u \notin x \cap z$.
            Hence $u \in x \symdiff y$.
            Thus $u \in x \cup y$ and $u \notin x \cap y$.
            Therefore $u \in x$ or $u \in y$.
            Then we have the thesis.
          End.
        End.
      End.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_496712}
      \[ x \symdiff x = \emptyset. \]
    \end{proposition}
    \begin{proof}
      $x \symdiff x
      = (x \cup x) \setminus (x \cap x)
      = x \setminus x
      = \emptyset$.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_182395}
      \[ x \symdiff \emptyset = x. \]
    \end{proposition}
    \begin{proof}
      $x \symdiff \emptyset
      = (x \cup \emptyset) \setminus (x \cap \emptyset)
      = x \setminus \emptyset = x$.
    \end{proof}


    \begin{proposition}\label{SetTheory_01_04_814558}
      \[ x = y \iff x \symdiff y = \emptyset. \]
    \end{proposition}
    \begin{proof}
      Case $x = y$.
        Then $x \symdiff y = (x \cup x) \setminus (x \cap x) = x \setminus x =
        \emptyset.$
        Hence the thesis.
      End.

      Case $x \symdiff y = \emptyset$.
        Then $(x \cup y) \setminus (x \cap y)$ is empty.
        Hence every element of $x \cup y$ is an element of $x \cap y$.
        Thus for all elements $u$ if $u \in x$ or $u \in y$ then $u \in x$ and
        $u \in y$.
        Therefore every element of $x$ is an element of $y$.
        Every element of $y$ is an element of $x$.
        Then we have the thesis.
      End.
    \end{proof}
  \end{forthel}
\end{document}
